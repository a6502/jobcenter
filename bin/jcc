#!/usr/bin/env perl

no lib '.';

# mojo
use Mojo::Base -base;
use Mojo::Util qw(decode);
#use Mojo::IOLoop;
use Mojo::Pg;

# stdperl
use Cwd qw(realpath);
use Data::Dumper;
use FindBin;
use Getopt::Long qw();
use Time::HiRes qw(time);

# cpan
use Config::Tiny;
use Pegex::Input;
use Pegex::Parser;
use Text::Table::Tiny 'generate_table';

# JobCenter
use lib realpath("$FindBin::Bin/../lib");
use JobCenter::JCC::CodeGenerator;
use JobCenter::JCC::Grammar;
use JobCenter::JCC::Receiver;
use JobCenter::JCC::VersionChecker;
use JobCenter::Util qw(slurp);

exit main(@ARGV);

sub main {
	my $config = realpath("$FindBin::Bin/../etc/jobcenter.conf");
	my $debug = 1;
	my $dry_run = 0;
	my $help = 0;
	my $parseonly = 0;
	my $tags;

	Getopt::Long::Configure(qw( bundling posix_default ));
	Getopt::Long::GetOptionsFromArray(
		\@_,
		'config=s' => \$config,
		'debug|d' => \$debug,
		'dry-run|n' => \$dry_run,
		'help|h' => \$help,
		'parseonly|p' => \$parseonly,
		'tags=s' => \$tags
	) or return usage();
	return usage() if $help;

	my $wff = shift;
	return usage() unless $wff;
	
	my $wfsrc = decode('UTF-8', slurp($wff));
	
	my $grammar = JobCenter::JCC::Grammar->new;

	print Dumper($grammar->tree);

	my $receiver = JobCenter::JCC::Receiver->new;

	my $parser = Pegex::Parser->new(
		grammar => $grammar,
		receiver => $receiver,
		debug => $debug,
	);

	my $start = time();
	my $wfast = $parser->parse(Pegex::Input->new(stringref => \$wfsrc));
	my $took = time() - $start;

	print Dumper($wfast);
	print Dumper($receiver->{labels});
	printf("took %0.3f\n", $took);
	return 0 if $parseonly;

	# what do we call ourselves
	my $clientname = "$0 [$$]";

	# make our application_name in postgresql our clientname
	$ENV{'PGAPPNAME'} = $clientname;

	my $cfg = Config::Tiny->read("$FindBin::Bin/../etc/jobcenter.conf");
	die 'Config Errror: ' . Config::Tiny->errstr() unless $cfg;
	#print Dumper($cfg);

	my $pg = Mojo::Pg->new(
		'postgresql://'
		. $cfg->{admin}->{user}
		. ':' . $cfg->{admin}->{pass}
		. '@' . ( $cfg->{pg}->{host} // '' )
		. ( ($cfg->{pg}->{port}) ? ':' . $cfg->{pg}->{port} : '' )
		. '/' . $cfg->{pg}->{db}
	);

	my $jcg = JobCenter::JCC::CodeGenerator->new(
		db => $pg->db,
		debug => $debug,
		dry_run => $dry_run,
	);

	$jcg->generate(
		wfsrc => \$wfsrc,
		wfast => $wfast,
		labels => $receiver->labels,
		tags => $tags,
	);

	reference_version_check($pg, $wfast);

	say "the end is nigh!";
	return 0;
}

# check the versions of references to newly compiled
sub reference_version_check {
	my ($pg, $wfast) = @_;

	# extract the workflow details
	my ($wf) = values %$wfast;

	my $jvc = JobCenter::JCC::VersionChecker->new(db => $pg->db);

	# find workflows referencing this out of date action / workflow
	my $res = $jvc->out_of_date(actions => [$wf->{workflow_name}]);

	if ($res->rows) {

		my @cols = qw/
			workflow_name 
			workflow_id 
			action_id 
			version 
			latest_action_id 
			latest_version
		/;

		my $body = $res->hashes->map(sub { [ @{$_}{@cols} ] });
		my $rows = [ \@cols, @{$body->to_array} ];

		say "";
		say "stale references found for \"$wf->{workflow_name}\":";
		say generate_table(rows => $rows, header_row => 1);
		say "";
	}
}

sub usage {
	print <<'EOT';
jcc [options] <filename>
	--config=<file>	specify config file
-d	--debug		enable debugging
-n	--dry-run       do not commit
-p	--parseonly	stop after parsing
	--tags		':' seperated version tags
EOT
	return 1;
}

