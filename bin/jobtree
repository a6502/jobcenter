#!/usr/bin/env perl

use strict;
use warnings;
use 5.10.0;

# standard modules
use Data::Dumper;
use FindBin;

# non standard modules
use Config::Tiny;
use DBI;
use DBD::Pg qw(:async);
use Getopt::Long qw(GetOptionsFromArray);
use Time::Seconds;

use constant DEFAULT_LIMIT => 1000;
use constant RIGHT_ALIGN => { duration => 1 };

sub job_states;

my $pgh;

exit main(@ARGV);

sub help {
	print <<__END;
usage:

    $FindBin::Script [options] job_ids

options:

    -h, --help      show this help message
    -d, --head      only show the head of the job tree
    -j, --job-id    return only the job_ids
    -a, --all       all jobs, including inactive jobs
    -s, --state     show jobs in states, comma seperated
    -x, --exclude   exclide jobs in states, comma seperated
    -l, --limit     default limit is ${\DEFAULT_LIMIT} jobs
__END
}

sub main {

	GetOptionsFromArray(\@_, \my %opts, 
		'help|h', 
		'head|d',
		'job-id',
		'all',
		'state=s',
		'exclude|x=s',
		'limit=i',
	) or die "unknown option";

	$opts{limit} //= DEFAULT_LIMIT;

	if ($opts{help}) {
		help();
		return 0;
	}

	die "expects limit as positive number: $opts{limit}\n" if $opts{limit} <= 0;

	for (@_) {
		die "expects job_id as number: $_\n" unless /^\d+$/;
	}

	my $cfgpath = "$FindBin::Bin/../etc/jobcenter.conf";

	my $cfg = Config::Tiny->read($cfgpath);
	die "failed to read config $cfgpath: ${\Config::Tiny->errstr}\n" unless $cfg;
	my $pgdsn = 'dbi:Pg:dbname=' . $cfg->{pg}->{db}
		. (($cfg->{pg}->{host}) ? ';host=' . $cfg->{pg}->{host} : '')
		. (($cfg->{pg}->{port}) ? ';port=' . $cfg->{pg}->{port} : '');
	my $pguser = $cfg->{admin}->{user};
	my $pgpass = $cfg->{admin}->{pass};

	# make our clientname the application_name visible in postgresql
	$ENV{'PGAPPNAME'} = "$0 [$$]";

	$pgh = DBI->connect(
		$pgdsn, $pguser, $pgpass,
		{
			AutoCommit => 1,
			RaiseError => 1,
			PrintError => 0,
		}
	) or die "cannot connect to db: $DBI::errstr";

	$opts{state} //= '';
	$opts{state} =~ s/(?:^\s+\|\s+$)//g;

	my %states;

	for (split /\s*,\s*/, $opts{state}) {

		next if $_ eq '';

		my $state = lc($_);

		die "unknown state: $_\n" unless job_states->{$state};

		$states{$state}++;
	}

	$opts{exclude} //= '';
	$opts{exclude} =~ s/(?:^\s+\|\s+$)//g;

	my %exclude;

	for (split /\s*,\s*/, $opts{exclude}) {

		next if $_ eq '';

		my $state = lc($_);

		die "unknown state: $_\n" unless job_states->{$state};

		$exclude{$state}++;
	}

	$opts{state}   = \%states;
	$opts{exclude} = \%exclude;

	my $job_tree = job_tree(\%opts, \@_);

	if ($opts{head}) { 
		@$job_tree = grep { not defined $_->{parentjob_id} } @$job_tree 
	}

	if ($opts{'job-id'}) {
		for (sort { $a->{job_id} <=> $b->{job_id} } @$job_tree) {
			say $_->{job_id};
		}
	} else {
		table($job_tree);
	}

	return 0;
}

my $job_states;
sub job_states () {

	return $job_states if $job_states;

	my $sql = q[
		SELECT 
			e.enumlabel enum_value
		FROM 
			pg_type t
		JOIN 
			pg_enum e 
		ON 
			t.oid = e.enumtypid
		JOIN 
			pg_catalog.pg_namespace ns
		ON 
			ns.oid = t.typnamespace 
		WHERE 
			ns.nspname = 'jobcenter' AND 
			t.typname = 'job_state'
	];

	my $sth = $pgh->prepare_cached($sql) or 
		die "failed to prepare job_states query: ".$pgh->errstr;

	$sth->execute() or 
		die "failed to get job_states: ".$pgh->errstr;

	$sth->bind_columns(\my $val);

	$job_states = {};

	while ($sth->fetch) {
		$job_states->{$val} = 1;
	}

	return $job_states;
}

sub job_tree {
	my ($opts, $job_ids) = @_;

	my $job_qs     = join ',', ('?')x(@$job_ids);
	my $state_qs   = join ',', ('?')x(keys %{$opts->{state}});
	my $exclude_qs = join ',', ('?')x(keys %{$opts->{exclude}});

	my $sql = qq[
		WITH recursive 
		${\($job_qs ? qq[
		${\($opts->{all} ? qq[
		find_archive_parents AS (
			SELECT 
				job_id,
				parentjob_id,
				'jobs_archive' source,
				array[job_id] found
			FROM 
				jobs_archive
			WHERE
				job_id IN ($job_qs)
			UNION ALL
			SELECT 
				ja.job_id,
				ja.parentjob_id,
				'jobs_archive' source,
				fap.found || ja.job_id found
			FROM
				jobs_archive ja
			JOIN
				find_archive_parents fap
			ON 
				ja.job_id = fap.parentjob_id AND
				ja.job_id <> ALL (fap.found) -- loop guard
		), 
		] : qq[])}
		find_parents AS (
			SELECT 
				job_id,
				parentjob_id,
				'jobs' source,
				array[job_id] found
			FROM 
				jobs
			WHERE
				job_id IN ($job_qs)
			${\($opts->{all} ? qq[
			UNION ALL
			SELECT
				*
			FROM 
				find_archive_parents
			] : qq[])}
			UNION ALL
			SELECT 
				j.job_id,
				j.parentjob_id,
				'jobs' source,
				fp.found || j.job_id found
			FROM 
				jobs j
			JOIN
				find_parents fp
			ON
				j.job_id = fp.parentjob_id AND
				j.job_id <> ALL (fp.found) -- loop guard
		),
		] : qq[])}
		heads AS (
			SELECT
				job_id,
				${\($job_qs ? qq[source] : qq['jobs'::text source])}
			FROM 
				${\($job_qs ? qq[find_parents] : qq[jobs])}
			WHERE 
				parentjob_id IS NULL
		), tree AS (
			SELECT
				h.job_id,
				null::bigint parentjob_id,
				array[h.job_id] found,
				h.job_id head
			FROM
				heads h
			WHERE
				h.source = 'jobs'
			UNION ALL
			SELECT 
				j.job_id, 
				j.parentjob_id,
				t.found || j.job_id found,
				t.head
			FROM 
				jobs j 
			JOIN 
				tree t
			ON
				j.parentjob_id = t.job_id AND
				j.job_id <> ALL (t.found) -- loop guard
		), big_tree AS (
			${\($opts->{all} ? qq[
			SELECT
				h.job_id,
				null::bigint parentjob_id,
				array[h.job_id] found,
				h.job_id head
			FROM
				heads h
			WHERE
				h.source = 'jobs_archive'
			UNION ALL
			] : qq[])}
			SELECT
				*
			FROM
				tree
			${\($opts->{all} ? qq[
			UNION ALL
			SELECT 
				ja.job_id, 
				ja.parentjob_id,
				bt.found || ja.job_id found,
				bt.head
			FROM 
				jobs_archive ja 
			JOIN 
				big_tree bt
			ON
				ja.parentjob_id = bt.job_id AND
				ja.job_id <> ALL (bt.found) -- loop guard
			] : qq[])}
		), job_tree AS (
			SELECT 
				bt.job_id, 
				bt.head,
				j.workflow_id, 
				j.parentjob_id,
				bt.found,
				j.job_finished,
				j.state,
				EXTRACT(EPOCH FROM age(COALESCE(j.job_finished, now()), j.job_created)) duration
			FROM 
				big_tree bt
			JOIN
				jobs j
			ON
				bt.job_id = j.job_id
			${\($opts->{all} ? qq[
			UNION ALL
			SELECT 
				bt.job_id, 
				bt.head,
				ja.workflow_id, 
				ja.parentjob_id,
				bt.found,
				ja.job_finished,
				ja.state,
				EXTRACT(EPOCH FROM age(COALESCE(ja.job_finished, now()), ja.job_created)) duration
			FROM 
				big_tree bt
			JOIN
				jobs_archive ja
			ON
				bt.job_id = ja.job_id
			] : qq[])}
		)
		SELECT 
			jt.job_id, 
			jt.workflow_id, 
			jt.parentjob_id,
			jt.job_finished,
			jt.state,
			jt.duration,
			a.name
		FROM 
			job_tree jt
		LEFT JOIN
			actions a
		ON 
			jt.workflow_id = a.action_id
		WHERE 
			${\($opts->{head} ? qq[ jt.head = jt.job_id AND ]           : qq[])}
			${\(!$opts->{all} ? qq[ jt.job_finished IS NULL AND ]       : qq[])}
			${\($exclude_qs   ? qq[ jt.state NOT IN ($exclude_qs) AND ] : qq[])}
			${\($state_qs     ? qq[ jt.state IN ($state_qs) AND ]       : qq[])}
			TRUE
		ORDER BY 
			jt.head ASC, 
			jt.job_id ASC 
		LIMIT 
			$opts->{limit}
	];

	my $sth = $pgh->prepare_cached($sql) or 
		die "failed to prepare job_tree query: ".$pgh->errstr;

	$sth->execute(
		(@$job_ids)x($opts->{all} ? 2 : 1),
		keys %{$opts->{exclude}}, 
		keys %{$opts->{state}}, 
	) or die "failed to get job_tree: ".$pgh->errstr;

	my @rows;
	while (my $row = $sth->fetchrow_hashref) {
		push @rows, {
			duration => format_duration($row->{duration}),
			map { $_ => $row->{$_} } 
			qw/
				job_id 
				parentjob_id 
				job_finished 
				state 
				name 
				workflow_id
			/
		};
	}

	return \@rows;
}

sub format_duration {
	my $t = Time::Seconds->new(shift);
	my $sign = '';
	if ($t < 0) {
		$t   *= -1;
		$sign = "- ";
	}
	my ($n, @d);
	for (
		[ONE_DAY,    'days',    'd'],
		[ONE_HOUR,   'hours',   'h'],
		[ONE_MINUTE, 'minutes', 'm'],
		[0,          'seconds', 's'],
	) {
		my ($one, $method, $unit) = @$_;
		if ($t >= $one) {
			$n = sprintf("%2d", $t->$method);
			push @d, "$n$unit";
			$t -= ($n * $one || 1); # XXX make sure seconds shown
		}
	}
	return $sign . join " ", @d;
}

sub table {
	my $job_tree = shift;

	my %relations; 
	my %jobs;
	my %heads;
	for (@$job_tree) {
		$relations{$_->{parentjob_id}//''}{$_->{job_id}}++;
		$jobs{$_->{job_id}} = $_;
		$heads{$_->{job_id}}++ unless defined $_->{parentjob_id};
	}

	my %headings = (
		job_id   => 'JOB_ID',
		duration => 'DURATION',
		state    => 'JOB_STATE',
		name     => 'WORKFLOW',
	);

	my %max;
	for my $row (\%headings, @$job_tree) {
		for (qw/job_id duration state/) {
			my $l = length $row->{$_};
			if (not defined $max{$_} or $l > $max{$_}) {
				$max{$_} = $l;
			}
		}
	}

	for (qw/job_id duration state name/) {
		if (my $m = $max{$_}) {
			if (RIGHT_ALIGN->{$_}) {
				printf "%${m}s  ", $headings{$_};
			} else {
				printf "%-${m}s  ", $headings{$_};
			}
		} else {
			print $headings{$_};
		}
	}

	print "\n";

	_table('', '', [ sort { $a <=> $b } keys %heads ], \%max, \%relations, \%jobs);
}

# recursive helper to output table
sub _table {
	my ($indent1, $indent2, $job_ids, $max, $relations, $jobs) = @_;

	for (my $i = 0; $i < @$job_ids; $i++) {

		my $job_id = $job_ids->[$i];

		my $top_level      = ($indent1 eq '' and $indent2 eq '');
		my $has_children   = $relations->{$job_id} ? 1 : 0;
		my $has_more_peers = ($i + 1 <= $#$job_ids);

		my $row = $jobs->{$job_id} or die "something went wrong!";

		for (qw/job_id duration state/) {

			if (my $m = $max->{$_}) {

				if (RIGHT_ALIGN->{$_}) {
					printf "%${m}s  ", $row->{$_};
				} else {
					printf "%-${m}s  ", $row->{$_};
				}
			}
		}

		print "$indent1$indent2$row->{name} ($row->{workflow_id})\n";

		my $indent3 = $indent1;

		if ($top_level) {
			$indent3 .= '';
		} elsif ($has_children and $has_more_peers) {
			$indent3 .= ' |  ';
		} else {
			$indent3 .= '    ';
		}

		if (my $children = $relations->{$job_id}) {

			my @children = ( sort { $a <=> $b } keys %$children );

			_table($indent3, ' \_ ', \@children, $max, $relations, $jobs);
		}
	}
}

