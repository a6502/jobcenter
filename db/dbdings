#!/usr/bin/env perl

# mojo
use Mojo::Base -base;
use Mojo::Pg;

# stdperl
use Cwd qw(realpath);
use Data::Dumper;
use Digest::MD5 qw(md5_hex);
use FindBin;
use Data::Dumper;

# other
#use Algorithm::Diff;
use Config::Tiny;

# JobCenter
use lib realpath("$FindBin::Bin/../lib");
use JobCenter::Util qw( slurp );


exit main(@ARGV);

# ugly but handy globals
our ($cfg, $pg, $allprocsq, $procsrcq, $reposchema, $dbschema, %maestro);
#our (%db_src, %file_src);

sub main {
	my %actions = (
		'check' => \&checkschema,
		'compare' => \&compprocs,
		'create' => \&createschema,
		'downgrade' => \&downgradeschema,
		#'dump' => \&dumpprocs, 
		'help' => \&help,
		'load' => \&loadproc,
		'save' => \&saveproc,
		'seccheck' => \&seccheck,
		'secfix' => \&secfix,
		'update' => \&updateprocs,
		'upgrade' => \&upgradeschema,
		'verify' => \&verifyschema,
	);
	
	my %commands = (
		'compprocs' => \&compprocs,
		'loadproc' => \&loadproc,
		'saveproc' => \&saveproc,
	);
	
	($0) = ($0 =~ m|/?(\w+)$|);

	my $handler = $commands{$0};

	$handler = $actions{shift @_ // 'help'} unless $handler;

 	die "$0: don't know what to do" unless $handler;

	$cfg = Config::Tiny->read("$FindBin::Bin/../etc/jobcenter.conf");
	#$cfg = Config::Tiny->read('../etc/jc2.conf');
	die 'Config Errror: ' . Config::Tiny->errstr() unless $cfg;
	#print Dumper($cfg);

	$ENV{'PGAPPNAME'} = $0;
	$pg = Mojo::Pg->new(
		'postgresql://'
		. $cfg->{admin}->{user}
		. ':' . $cfg->{admin}->{pass}
		. '@' . ( $cfg->{pg}->{host} // '' )
		. ( ($cfg->{pg}->{port}) ? ':' . $cfg->{pg}->{port} : '' )
		. '/' . $cfg->{pg}->{db}
	);

	chdir($FindBin::Bin) or die "cannot chdir to $FindBin::Bin";

	return &$handler(@_);
}

sub help {
	print <<'EOF';
dbdings usage: dbdings <command>
Schema management commands:
	check:           check schema version in db and repo
	create:          create schema in db
	downgrade <ver>: try to downgrade db schema version to <ver>
	upgrade:         try to upgrade db schema version to repo version
	verify:          verify latest schema upgrade
Stored procedure management commands:
	compare:         compare version between db and repo
	load <proc>:     load <proc> from repo into db
	save <proc>:     save <proc> from db to repo
	update:          update procs in db from repo
Security commands:
	seccheck:        check security of stored procedures
	secfix:          fix security of stored procedures

EOF
}

sub checkdbschema {
	my $res;
	my $db = $pg->db;
	{
		# don't panic if the _schema table does not exist yet
		local $db->dbh->{RaiseError} = 0;
		#local $db->dbh->{PrintError} = 1;
		$res = $db->query(q|select version from _schema|);
	}
	if ($res->sth->err) {
		say "db schema version not found: ", $res->sth->errstr;
		return undef;
	}
	$res = $res->array;
	$res = $$res[0];
	say 'db schema version ', $res // 'undef';
	$dbschema = $res;
}

sub checkreposchema {
	open my $vf, '<', 'schema/.version' or die "no schema/.version file";
	my $v = <$vf>;
	close $vf;
	chomp $v;
	die "weird version $v" unless $v =~ /^\d+$/;
	say "found repo schema version $v";
	$reposchema = $v;
}


sub findschemaupgrades {
	my @scripts = sort grep { -f $_ or die "$_ not found" } <schema/v*/{alter,verify}.sql>;
	#say 'found scripts: ', join(', ', @scripts);
	@scripts = grep { my ($v) = $_ =~ m|/v([0-9]+)/|; $v > $dbschema and $v <= $reposchema } @scripts;
	#say 'found scripts: ', join(', ', @scripts);
	return @scripts;
}

sub checkschema {
	checkdbschema();
	checkreposchema();
	unless (defined $dbschema) {
		die "need to create the schema from scratch\n" .
		    "call $0 create to do that";
	}
	if ($reposchema < $dbschema) {
		die "fileschema is lower than dbschema ($reposchema < $dbschema)\n" .
		    "downgrade dbschema before reverting repo";
	}
	if ($reposchema > $dbschema) {
		my @scripts = findschemaupgrades;
		die 'upgrade scripts not found?' unless @scripts;
		say 'need to do scripts ', join(', ', @scripts), ' for upgrade';
		return 1;
	}		
	if ($reposchema == $dbschema) {
		say "schema is up to date";
		return 0;
	}
	die "how did we get here?";
}

sub createschema {
	checkdbschema();
	die "db already exists?" if $dbschema;
	checkreposchema();
	say "need to create schema v$reposchema";
	
	# find the most recent create scripts
	my @scripts = (
		grep { $_ } # only defined items
		map {
			# try to find create script in dir
			my @f = grep { -f $_ } <$_/{create{1,2},verify}.sql>;
			if ($#f == 2) {
				[ $f[0], $f[1], $f[2] ]
			} else {
				#say 'no scripts in ', $_;
				undef;
			}
		}
		sort {($a =~ /v(\d+)$/)[0] <=> ($b =~ /v(\d+)$/)[0]} # numerically in ascending order
		grep {
			my ($v) = $_ =~ m|^schema/v([0-9]+)$|;
			-d $_ and $v and $v <= $reposchema
		}
		<schema/v*>
	)[-1]; # we only want the newest version create scripts
	#print "scripts: ", Dumper(\@scripts);
	#exit;

	($dbschema) = ($scripts[0][0] =~ m|^schema/v([0-9]+)/|) or die "cannot find script version";
	
	say 'need to do ', join(', ', map { (ref) ? join(', ', @$_) : $_ } @scripts);
	
	my @sqls;
	for (@scripts) {
		if (ref) {
			push @sqls, sqlfile(@$_[0]); # create1
			push @sqls, procs("schema/v$dbschema/procs");
			push @sqls, sqlfile(@$_[1]); # create2
			push @sqls, sqlfile(@$_[2]); # verify
		} else {
			push @sqls, sqlfile($_);
		}
	}
	my $res = execsql(\@sqls);
	die 'create failed' if $res;

	if ($dbschema lt $reposchema) {
		say "created schema version $dbschema, now upgrading to $reposchema";
		return upgradeschema();
	}
	say "created schema version $dbschema";
	return 0;
}

sub upgradeschema {
	unless (checkschema()) {
		say "nothing to upgrade?";
		return 0;
	}
	my @scripts = findschemaupgrades($reposchema, $dbschema);
	
	say 'need to do ', join(', ', @scripts);

	my @sqls;
	push @sqls, sqlfile($_) for @scripts;
	my $res = execsql(\@sqls);
	
	die "upgrade failed" if $res;

	$pg->db->query(q|update _schema set version = $1|, $reposchema);
	# FIXME: check update?

	say "upgraded to $reposchema";
	return 0	
}	

sub downgradeschema {
	my $want = shift;
	die "downgrade to what version?" unless $want;
	checkschema();
	die "want version bigger than repo version" if $want ge $reposchema;
	if ($dbschema eq $want) {
		say "already on db schema version $dbschema, nothing to do";
		return 0;
	}

	my @scripts = sort grep { -f $_ } <schema/v*/downgrade.sql>;
	@scripts = grep { my ($v) = $_ =~ m|/v([0-9.]+)/|; $v gt $want and $v <= $dbschema } @scripts;
	die 'nothing to do?' unless @scripts;
	say 'need to do ', join(', ', @scripts);

	# FIXME: check that downgrades form a sequence

	my @sqls;
	push @sqls, sqlfile($_) for @scripts;
	my $res = execsql(\@sqls);
	
	die "downgrade failed" if $res;

	$pg->db->query(q|update _schema set version = $1|, $want);
	# FIXME: check update?

	say "downgraded to $want";
	return 0
}


sub verifyschema {
	if (checkschema() != 0) {
		say "nothing to verify? may need to upgrade first";
		return 0;
	}

	my @scripts =  sort <schema/v$reposchema/verify.sql>;

	say 'need to do ', join(', ', @scripts);

	my @sqls;
	push @sqls, sqlfile($_) for @scripts;

	my $res = execsql(\@sqls);
	die "verify failed" if $res;

	return 0;
}	


sub execsql {
	my ($sqls, $check) = @_;

	$check = ($check) ? 'true' : 'false';

	unshift @$sqls, \<<"EOF";

-- default header
SET client_encoding = 'UTF8';
SET standard_conforming_strings = on;
SET client_min_messages = warning;
SET check_function_bodies = $check;
SET plpgsql.extra_warnings TO 'shadowed_variables';
SET statement_timeout = 0;
SET lock_timeout = 0;
SET row_security = off;
-- end of default header
EOF

	#say "sql now: ", $sql;
	
	$ENV{'PGPASSWORD'} = $cfg->{admin}->{pass};
	my @hostport;
	push @hostport, ('-h', $cfg->{pg}->{host}) if $cfg->{pg}->{host};
	push @hostport, ('-p', $cfg->{pg}->{port}) if $cfg->{pg}->{port}; 
	open my $fh, '|-', 'psql', '-w', '-e', '-1',
			'-v', 'ON_ERROR_STOP=1',
			@hostport,
			'-d', $cfg->{pg}->{db},
			'-U', $cfg->{admin}->{user}
		or die "cannot start psql: $!";
	print $fh $$_ for @$sqls;
	close $fh;

	return $?;
}

sub sqlfile {
	my $f = shift;
	#my $dbname = $cfg->{pg}->{db};
	#my $cluster = $cfg->{pg}->{cluster};

	my $sql = slurp $f;
	
	my %subs = (
		'$JCADMIN' => ($cfg->{admin}->{user} or die "foo"),
		'$JCCLIENT' => ($cfg->{client}->{user} or die "fooo"),
		'$JCMAESTRO' => ($cfg->{maestro}->{user} or die "foooo"),
		'$JCSYSTEM' => ($cfg->{system}->{role} or die "fooooo"),
		'$JCPERL' => ($cfg->{perl}->{role} or die "fooooop")
	);

	while ( my($k,$v) = each(%subs) ) {
		$sql =~ s/\Q$k\E/$v/g;
	}

	return \$sql;
}

sub procs {
	my $procdir = shift;
	my $jcsystem = ($cfg->{system}->{role} or die "fooooo"); 
	my @funcs;

	for my $p (<$procdir/*.sql>) {
		say "doing $p";
		my $func .= slurp $p;
		my ($funcname) = $func  =~ /FUNCTION\s+(.*)\s+RETURNS/ms;
		die "cannot find funcname in $p" unless $funcname;
		# ugh.. we need to filter out default arguments here
		# let's try a quick hack:
		$funcname =~ s/ DEFAULT .*?(,|\))/$1/g;
		
		#$func .= ';'
		$func .= <<EOF;
;
ALTER FUNCTION $funcname SET search_path = jobcenter, pg_catalog, pg_temp;
ALTER FUNCTION $funcname OWNER TO $jcsystem;

EOF
		push @funcs, \$func;

	}

	return @funcs;
}

sub dumpprocs {
	die "are you sure?";
	my $funcs = $pg->db->dollar_only->query($allprocsq, $cfg->{pg}->{db});
	
	while (my $f = $funcs->hash) {
		say "dumping $f->{name}";
		open my $o, '>', "procs/$f->{name}.sql" or die "urgh";
		print $o $f->{code};
		close $o; 
	}

	return 0;
}

sub compprocs {
	my @filefuncs = sort grep { -f $_ } <procs/*.sql>;

	my $diffs = 0;
				
	foreach my $fn (@filefuncs) {
		#say "fn: $fn";
		my ($f) = $fn =~ m|^procs/(.*)\.sql$|;
		my $dbmd5 = db_md5($f);
		unless ($dbmd5) {
			#warn "no proc $f in the db?";
			$diffs++;
			next
		}
		my $procmd5 = proc_md5($f);
		unless ($procmd5) {
			#warn "no proc $f in the procs table?";
			$diffs++;
			next;
		}
		my $filemd5 = file_md5($fn);
		if ($procmd5 ne $dbmd5 and $dbmd5 ne $filemd5 and $filemd5 ne $procmd5) {
			say "proc $f differs between db and procs table and file";
			$diffs += 3;
		} elsif ($dbmd5 eq $filemd5 and $filemd5 ne $procmd5) {
			say "proc $f procs table out of sync";
			$diffs++;
		} elsif ($filemd5 ne $procmd5) {
			say "proc $f differs between procs table and file";
			$diffs++;
		} elsif ($filemd5 ne $dbmd5) {
			say "proc $f differs between db and file";
			#diff($file_src{$fn}, $db_src{$f});
			$diffs++;
		} else {
			#say " (did $f)";
		}
	}

	if ($diffs) {
		say "found $diffs differences";
	} else {
		say "no differences found";
	}

	return 0;
}

sub updateprocs {
	checkschema();
	die "dbschema $dbschema not equal to reposchema $reposchema." if $dbschema ne $reposchema;	
	
	#hmm.. reuse comprocs somehow?
	my @filefuncs = sort grep { -f $_ } <procs/*.sql>;

	my $updated = 0;
			
	foreach my $fn (@filefuncs) {
		#say "fn: $fn";
		my ($f) = $fn =~ m|^procs/(.*)\.sql$|;
		my $dbmd5 = db_md5($f);
		my $procmd5 = proc_md5($f);
		my $filemd5 = file_md5($fn);
		if (not $dbmd5 and not $procmd5) {
			say "loading new proc $f";
			loadproc($f);
			$updated++;
		} elsif ($dbmd5 and not $procmd5) {
			die "proc $f exists in db but not in procs table.\n".
			    "manual cleanup required.";
		} elsif (not $dbmd5 and $procmd5) {
			die "proc $f exists in procs table but not not in db.\n".
			    "manual cleanup required.";
		} elsif ($procmd5 ne $dbmd5 and $dbmd5 ne $filemd5 and $filemd5 ne $procmd5) {
			die "proc $f differs between db and procs table and file.\n".
			    "manual cleanup required.";
		} elsif ($dbmd5 ne $procmd5 and $dbmd5 eq $filemd5) {
			say "proc md5sum out of sync, updating";
			loadproc($f);
			$updated++;
		} elsif ($dbmd5 ne $procmd5) {
			die "proc $f differs between db and procs table.\n".
			    "manual cleanup required.";
		} elsif ($filemd5 ne $dbmd5) {
			say "loading modified proc $f from file";
			loadproc($f);
			$updated++;
		} else {
			# up to date ..
			#say " (did $f)";
		}
	}

	if ($updated) {
		say "updated $updated stored procedures";
	} else {
		say "no stored procedures were updated";
	}

	return 0;
}

sub loadproc {
	my $proc = shift;
	die "usage: loadproc <procname>" unless $proc;
	$proc = $1 if $proc =~ m|(?:procs/)?(.*)\.sql$|;
	die "$proc not found in procs dir" unless -f "procs/$proc.sql";

	my $procmd5 = proc_md5($proc);

	# get the proc src
	my $dbprocsrc = $pg->db->dollar_only->query($procsrcq, $proc)->array;
	$dbprocsrc = ($dbprocsrc and @$dbprocsrc and $$dbprocsrc[0]) ? $$dbprocsrc[0] : undef;
	my $dbmd5 = ($dbprocsrc) ? md5_hex(mangle_proc_src($dbprocsrc)) : undef;
	my $procsrc = slurp("procs/$proc.sql");
	my $filemd5 = md5_hex(mangle_proc_src($procsrc));

	die "proc $proc exists in db and not in proc table, refusing to load" if $dbmd5 and not $procmd5;
	die "proc $proc was changed in the db, refusing to load" if $dbmd5 and $procmd5 and $procmd5 ne $dbmd5 and $dbmd5 ne $filemd5;

	if ($procmd5 and $filemd5 eq $procmd5) {
		say "$proc is alread up-to-date";
		return 0;
	}

	# get function name
	$procsrc =~ /^CREATE OR REPLACE FUNCTION\s+(.*)\n\s+RETURNS\s+(.*)\n/m or die 'cannot parse file function definition';
	my $func = $1;
	my $funcsig = "$1 RETURNS $2";
	# ugh.. we need to filter out default arguments here
	# let's try a quick hack:
	$func =~ s/ DEFAULT .*?(,|\))/$1/g;
	# and another hack to get just the types:
	pos $func = 0 while $func =~ s/(\(|, )\w+ (\w+(?:,|\)))/$1$2/g;
	#say "func now: $func";

	# get language
	$procsrc =~ /^ LANGUAGE (\w+)$/ms or die 'cannot find function language';
	my $lang = $1;
	#say "lang now: $lang";

	# get security definer
	my $secdef = ($procsrc =~ /^ SECURITY DEFINER$/ms) ? 1 : 0;

	if ($dbprocsrc) {
		# see if the function signature changed
		# we may need to drop the functio first
		$dbprocsrc =~ /^CREATE OR REPLACE FUNCTION\s+(.*)\n\s+RETURNS\s+(.*)\n/m or die 'cannot parse db function definition';
		my $dbfunc = $1;
		my $dbfuncsig = "$1 RETURNS $2";
		# ugh.. we need to filter out default arguments here
		# let's try a quick hack:
		$dbfunc =~ s/ DEFAULT .*?(,|\))/$1/g;

		if ($funcsig ne $dbfuncsig) {
			$procsrc = "DROP FUNCTION $dbfunc;\n" . $procsrc;
			$dbprocsrc = undef; # to set grants below
		}
	}

	unless ($dbprocsrc) {
		# now figure out the owner and grants
		my $JCSYSTEM = $cfg->{system}->{role} or die 'no system role?';
		my $JCPERL = $cfg->{perl}->{role} or die 'no perl role?';
		my $JCCLIENT = $cfg->{client}->{user} or die 'no client role?';
		my $JCMAESTRO = $cfg->{maestro}->{user} or die 'no maestro role?';
		my $tmpfunc = $func;
		$tmpfunc =~ s/^jobcenter\.//; 

		if ($lang eq 'plperl' and $secdef) {
			$procsrc .= <<"EOT";
;
ALTER FUNCTION $func OWNER TO $JCPERL;
REVOKE ALL ON FUNCTION $func FROM PUBLIC;
GRANT ALL ON FUNCTION $func TO $JCPERL;
EOT
		} elsif ($secdef and $maestro{$tmpfunc}) {
			$procsrc .= <<"EOT";
;
ALTER FUNCTION $func OWNER TO $JCSYSTEM;
REVOKE ALL ON FUNCTION $func FROM PUBLIC;
GRANT ALL ON FUNCTION $func TO $JCSYSTEM;
GRANT ALL ON FUNCTION $func TO $JCMAESTRO;
EOT
		} elsif ($secdef) {
			$procsrc .= <<"EOT";
;
ALTER FUNCTION $func OWNER TO $JCSYSTEM;
REVOKE ALL ON FUNCTION $func FROM PUBLIC;
GRANT ALL ON FUNCTION $func TO $JCSYSTEM;
GRANT ALL ON FUNCTION $func TO $JCCLIENT;
EOT
		} else {
			$procsrc .= <<"EOT";
;
ALTER FUNCTION $func OWNER TO $JCSYSTEM;
REVOKE ALL ON FUNCTION $func FROM PUBLIC;
GRANT ALL ON FUNCTION $func TO $JCSYSTEM;
EOT
		}
	}

	#$pg->db->dollar_only->query($procsrc);
	my $res = execsql( [ \$procsrc ], 1 ); # check function bodies
	die "loading $proc failed" if $res;
	
	if ($dbmd5) {
		$pg->db->dollar_only->query(q|update _procs set md5=$1 where "name"=$2|, $filemd5, $proc);
	} else {
		$pg->db->dollar_only->query(q|insert into _procs ("name", md5) values ($1, $2)|, $proc, $filemd5);
	}
}

sub saveproc {
	my $proc = shift;
	die "usage: saveproc <procname>" unless $proc;
	$proc = $1 if $proc =~ m|(?:procs/)?(.*)\.sql$|;
	my $flag = -f "procs/$proc.sql";
	warn "warnings: $proc not found in procs dir, new function?" unless $flag;

	my $dbprocsrc = $pg->db->dollar_only->query($procsrcq, $proc)->array;
	die "no proc $proc in the db?" unless $dbprocsrc and @$dbprocsrc and $$dbprocsrc[0];
	$dbprocsrc = $$dbprocsrc[0];

	my $dbmd5 = md5_hex($dbprocsrc);

	open my $procfd, '>', "procs/$proc.sql" or die "Cannot open $proc for writing: $!";
	print $procfd $dbprocsrc;
	close $procfd;

	if ($flag) {
		$pg->db->dollar_only->query(q|update _procs set md5=$1 where "name"=$2|, $dbmd5, $proc);
	} else {
		$pg->db->dollar_only->query(q|insert into _procs ("name", md5) values ($1, $2)|, $proc, $dbmd5);
	}
}

sub db_md5 {
	my $proc = shift;
	my $dbmd5 = $pg->db->dollar_only->query($procsrcq, $proc)->array;
	if ($dbmd5 and @$dbmd5 and $dbmd5->[0]) {
		my $src = mangle_proc_src($dbmd5->[0]);
		#$db_src{$proc} = $src;
		return md5_hex($src);
	}
	warn "no proc $proc in the db?";
	return undef;
}

sub proc_md5 {
	my $proc = shift;
	my $procmd5 = $pg->db->dollar_only->query(q|SELECT md5 FROM _procs WHERE "name" = $1|, $proc)->array;
	if ($procmd5 and @$procmd5 and $$procmd5[0]) {
		return $$procmd5[0];
	}
	warn "no func $proc in the procs table?";
	return undef;
}

sub file_md5 {
	my $f = shift;
	my $procsrc = slurp($f) or die "Cannot open $f for md5summing: $!";
	$procsrc = mangle_proc_src($procsrc);
	#$file_src{$f} = $procsrc;
	return md5_hex($procsrc);
}

# do horrible things here..
sub mangle_proc_src {
	my ($procsrc) = @_;
	if ($procsrc =~ s/\n SET search_path TO 'jobcenter', 'pg_catalog', 'pg_temp'\n/\n SET search_path TO jobcenter, pg_catalog, pg_temp\n/ms) {
		#say 'did some mangling!';
	}
	return $procsrc;
}

sub _seccheck {
	my $q = <<'EOT';
select
	p.oid,
	p.proname || coalesce((select '(' || string_agg(format_type(t, null), ', ') || ')' from unnest(p.proargtypes) as t), '()') as proc,
	pg_get_userbyid(proowner) as procowner,
	p.prosecdef,
	p.proacl,
	l.lanname
from
	pg_proc as p
	join pg_language as l on p.prolang = l.oid
where
	pronamespace = to_regnamespace('jobcenter')
EOT

	my $funcs = $pg->db->dollar_only->query($q);

	my $JCSYSTEM = $cfg->{system}->{role};
	my $JCPERL = $cfg->{perl}->{role};
	my $JCCLIENT = $cfg->{client}->{user};
	my $JCMAESTRO = $cfg->{maestro}->{user};
	my $sql = '';

	#print 'maestro: ', Dumper(\%maestro);

	while (my $f = $funcs->hash) {
		my $acl = $f->{proacl} ? join(',', @{$f->{proacl}}) : '';
		#print STDERR Dumper($f);
		if ($f->{lanname} eq 'plpgsql' or $f->{lanname} eq 'sql') {
			if ($f->{procowner} ne 'jc_system') {
				$sql .= <<"EOT";
ALTER FUNCTION $f->{proc} OWNER TO $JCSYSTEM;
EOT
			}
			if ($f->{prosecdef}) {
				if ($maestro{$f->{proc}}) {
					if ($acl ne "$JCSYSTEM=X/$JCSYSTEM,$JCMAESTRO=X/$JCSYSTEM") {
						$sql .= <<"EOT";
REVOKE ALL ON FUNCTION $f->{proc} FROM PUBLIC;
REVOKE ALL ON FUNCTION $f->{proc} FROM $JCCLIENT;
GRANT ALL ON FUNCTION $f->{proc} TO $JCSYSTEM;
GRANT ALL ON FUNCTION $f->{proc} TO $JCMAESTRO;
EOT
					}
				} elsif ($acl ne "$JCSYSTEM=X/$JCSYSTEM,$JCCLIENT=X/$JCSYSTEM") {
					$sql .= <<"EOT";
REVOKE ALL ON FUNCTION $f->{proc} FROM PUBLIC;
GRANT ALL ON FUNCTION $f->{proc} TO $JCSYSTEM;
GRANT ALL ON FUNCTION $f->{proc} TO $JCCLIENT;
EOT
				}
			} else {
				if ($acl ne "$JCSYSTEM=X/$JCSYSTEM") {
					$sql .= <<"EOT";
REVOKE ALL ON FUNCTION $f->{proc} FROM PUBLIC;
GRANT ALL ON FUNCTION $f->{proc} TO $JCSYSTEM;
EOT
				}
			}
		} elsif ($f->{lanname} eq 'plperl') {
			if ($f->{procowner} ne $JCPERL) {
				$sql .= <<"EOT";
ALTER FUNCTION $f->{proc} OWNER TO $JCPERL;
EOT
			}
			if ($acl ne "$JCPERL=X/$JCPERL") {
				$sql .= <<"EOT";
REVOKE ALL ON FUNCTION $f->{proc} FROM PUBLIC;
REVOKE ALL ON FUNCTION $f->{proc} FROM $JCSYSTEM;
ALTER FUNCTION $f->{proc} OWNER TO $JCPERL;
GRANT ALL ON FUNCTION $f->{proc} TO $JCPERL;
EOT
			}
		} elsif ($f->{lanname} eq 'plperlu') {
				die '-> plperlu should not be!';
		} else {
			die "uh? how did we get here?"
		}
	}
	return \$sql;
}

sub seccheck {
	my $sql = _seccheck();

	if ($$sql) {
		say "found security mismatch!";
		say "run $0 secfix to apply the following fixes:";
		say $$sql;
		return 1;
	}

	say "everything ok?";
	return 0;
}

sub secfix {
	my $sql = _seccheck();

	unless ($$sql) {
		say "nothing to fix!";
		return 0;
	}

	my $res = execsql( [ $sql ], 0 );
	die "secfix failed!" if $res;
}

=pod
sub diff {
	my ($a, $b) = @_;
	my @a = split /\n/, $a;
	my @b = split /\n/, $b;
	my $diff = Algorithm::Diff->new( \@a, \@b );
	$diff->Base(1);
	while(  $diff->Next()  ) {
		next if  $diff->Same();
		my $sep = '';
		if( ! $diff->Items(2) ) {
			printf "%d,%dd%d\n",
				$diff->Get(qw( Min1 Max1 Max2 ));
		} elsif( ! $diff->Items(1)  ) {
			printf "%da%d,%d\n",
			$diff->Get(qw( Max1 Min2 Max2 ));
		} else {
			$sep = "---\n";
			printf "%d,%dc%d,%d\n",
			$diff->Get(qw( Min1 Max1 Min2 Max2 ));
		}
		print "< $_\n"   for  $diff->Items(1);
		print $sep;
		print "> $_\n"   for  $diff->Items(2);
	}
}
=cut

# some queries
BEGIN {
$allprocsq = <<'EOT';
SELECT
  p.proname as "name",
  pg_catalog.pg_get_functiondef(p.oid) as "code"
FROM pg_catalog.pg_proc p
  LEFT JOIN pg_catalog.pg_namespace n ON n.oid = p.pronamespace
WHERE pg_catalog.pg_function_is_visible(p.oid)
  AND n.nspname = 'jobcenter'
  ORDER BY 1;
EOT

$procsrcq = <<'EOT';
SELECT
  pg_catalog.pg_get_functiondef(p.oid) as "code"
FROM pg_catalog.pg_proc p
  LEFT JOIN pg_catalog.pg_namespace n ON n.oid = p.pronamespace
WHERE pg_catalog.pg_function_is_visible(p.oid)
  AND n.nspname = 'jobcenter'
  AND p.proname = $1
EOT

# stored procedures called by the maestro (for loadproc/seccheck/secfix)
%maestro = map { $_ => 1 } qw[
	do_archival_and_cleanup(text)
	do_crash_recovery(text)
	do_jobtask(jobtask)
	do_jobtaskdone(jobtask)
	do_jobtaskerror(jobtask)
	do_ping(bigint)
	do_wait_for_children(jobtask)
	do_timeout(text)
];
# ugh
$maestro{'do_jobtaskerror(jobtask, boolean)'} = 1;

} #BEGIN

